# Responses after a partners takeout double

common (2w) X:"takeout" {
    3w:down => 12+ tpts, 4+M, 4+y;
        test AQxx xx Axxx Kxx:
	        (2H) X 3H;
    4M:down => 12+ tpts, 4+M;
        test AQxxx xx Axx Kxx:
	        (2H) X  4S;
    3N => 12+ tpts, stoppers w;
        test Axx AQx xxxx Kxx:
	        (2H) X 3N;
    2y:down => 7- tpts, 4+ y;
        test Axxx xxx xxx xxx:
	        (2H) X 2S;
    NJy:>2N:down => 8-11 tpts, 4+y;
        test xx Axxx Axxx xxx:
	        (2S) X 3H;    
    2N:"wk2leb" => always;
        test xxx Axxx xxx xxx:
	        (2S) X 2N;
	continuations {
	    3x:>3C => 5+ x, 18+ tpts;
	        test Ax xx AQJxxx AQx:
	            (2H) X 2N
	                 3D;
	    3N => balanced, stoppers w, 20+ tpts;
	        test AQx AQx Axx KJxx:
	            (2H) X 2N
	                 3N;
	    3C => always;
	        test Axxx xx AQx Axxx:
	            (2H) X 2N
	                 3C;
	    continuations {
	        3x => 4+ x;
		        test xxx Axxx xxx xxx:
			        (2S) X  2N
			             3C 3H;
	        P => 4+ C;
		        test xxx xxx xxx Axxx:
			        (2S) X  2N
			             3C P;
	    }
	}
}
