# Stayman over 1N

# C = ♣
# D = ♦
# H = ♥
# S = ♠

common [] 1N:"nt_opener" 2♣:"stayman" {

	[[Bid 4 card major]]
	2M => 4+M;
	    test Axx AQxx KJxx Qx:
	        1N 2♣
	        2♥;
	    test Axxx AQxx KJx Qx:
	        1N 2♣
	        2♥;
	    test Axxx AQx KJxx Qx:
	        1N 2♣
	        2♠;
	continuations {
	    [[splinter in support of ${M}]]
		4x:"splinter":<4M => gf+, 4+M, 1-x;
		    test AJx AQxx AQxxx x:
		        1N 2♣
		        2♥ 4♣;
		    test AJxx AQx AQxxx x:
		        1N 2♣
		        2♠ 4♣;
		    test AJxx x AQx AQxxx:
		        1N 2♣
		        2♠ 4♥;
		
		[[Invite slam in ${M}]]
		3OM => slaminv+, 4M;
		    test AJx Axxx Axx Axx:
		        1N 2♣
		        2♥ 3♠;
		    test AJxx Axx Axx Axx:
		        1N 2♣
		        2♠ 3♥;
		
		[[Invite game with 4 ${OM}]]
		2OM => inv, 4♠, 7- fit H;
		    test AJxx Axx xxx xxx:
		        1N 2♣
		        2♥ 2♠;
		
		[[Invite game in NT]]
		2N => inv, 7- fit M;
		    test AJx Axx xxxx xxx:
		        1N 2♣
		        2♥ 2N;
		
		[[Bid game in NT]]
		3N => gf, 7- fit M;
		    test AJxx AQx xxx xxx:
		        1N 2♣
		        2♥ 3N;
		
		[[Bid a new 5 card minor]]
		3m => gf+, 5+m, 7- fit M;
		    test AJx AQx AQxxx xx:
		        1N 2♣
		        2♥ 3♦;
	}

	[[Bid 2D with no 4 card major]]
	2♦ => 3- M;
	    test Axx AQx KJxx Qxx:
	        1N 2♣
	        2♦;
	continuations {
	    [[Bid 2${M} with invitational and 5+]]
		2M => inv, 5+M;
		    test AJxx Qxxxx xx xx:
		        1N 2♣
		        2♦ 2♥;
		    test AJxxx Qxxx xx xx:
		        1N 2♣
		        2♦ 2♠;
		
	    [[Invite game in NT]]
		2N => inv;
		    test AJxx Kxxx xxx xx:
		        1N 2♣
		        2♦ 2N;
		
	    [[Bid 3${M} with 4{$M} and 4{$OM} (smolen)]]
		3M => gf+, 4M, 5+OM;
		    test AJxxx AQxx xx xx:
		        1N 2♣
		        2♦ 3♥;
		    test AJxx AQxxx xx xx:
		        1N 2♣
		        2♦ 3♠;
		
	    [[Bid game in NT]]
		3N => gf;
		    test AJxx AQxx xxx xx:
		        1N 2♣
		        2♦ 3N;
		
	    [[Bid a 5 card minor and game force]]
		3m => gf+, 5+m;
		    test AJxx AQ AQxxx xx:
		        1N 2♣
		        2♦ 3♦;
	}
}
