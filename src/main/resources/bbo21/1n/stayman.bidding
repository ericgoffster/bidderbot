# Stayman over 1N

# C = ♣
# D = ♦
# H = ♥
# S = ♠

common [] 1N:"nt_opener" 2♣:"stayman" {

	[[Bid 4 card major]]
	2M => 4+M;
	    test Axx AQxx KJxx Qx:
	        1N 2♣
	        2♥;
	    test Axxx AQxx KJx Qx:
	        1N 2♣
	        2♥;
	    test Axxx AQx KJxx Qx:
	        1N 2♣
	        2♠;
	continuations {
		4m => gf+, 4+M, 1-m;
		    test AJx AQxx AQxxx x:
		        1N 2♣
		        2♥ 4♣;
		    test AJxx AQx AQxxx x:
		        1N 2♣
		        2♠ 4♣;
	
		4OM:<4M => gf+, 4+M, 1-OM;
		    test AJxx x AQx AQxxx:
		        1N 2♣
		        2♠ 4♥;
		
		3OM => slaminv+, 4M;
		    test AJx Axxx Axx Axx:
		        1N 2♣
		        2♥ 3♠;
		    test AJxx Axx Axx Axx:
		        1N 2♣
		        2♠ 3♥;
		
		2OM => inv, 4♠, 7- fit H;
		    test AJxx Axx xxx xxx:
		        1N 2♣
		        2♥ 2♠;
		
		2N => inv, 7- fit M;
		    test AJx Axx xxxx xxx:
		        1N 2♣
		        2♥ 2N;
		
		3N => gf, 7- fit M;
		    test AJxx AQx xxx xxx:
		        1N 2♣
		        2♥ 3N;
		
		3m => gf+, 5+m, 7- fit M;
		    test AJx AQx AQxxx xx:
		        1N 2♣
		        2♥ 3♦;
		
		4N => slaminv, 7- fit M;
		    test AJxx AQx AQx xxx:
		        1N 2♣
		        2♥ 4N;	
	}

	2♦ => always;
	    test Axx AQx KJxx Qxx:
	        1N 2♣
	        2♦;
	continuations {
		2M => inv, 5+M;
		    test AJxx Qxxxx xx xx:
		        1N 2♣
		        2♦ 2♥;
		    test AJxxx Qxxx xx xx:
		        1N 2♣
		        2♦ 2♠;
		
		2N => inv;
		    test AJxx Kxxx xxx xx:
		        1N 2♣
		        2♦ 2N;
		
		3M => gf+, 4M, 5+OM;
		    test AJxxx AQxx xx xx:
		        1N 2♣
		        2♦ 3♥;
		    test AJxx AQxxx xx xx:
		        1N 2♣
		        2♦ 3♠;
		
		3N => gf;
		    test AJxx AQxx xxx xx:
		        1N 2♣
		        2♦ 3N;
		
		3m => gf+, 5+m;
		    test AJxx AQ AQxxx xx:
		        1N 2♣
		        2♦ 3♦;
		
		4N => slaminv;
		    test AJxx AJxx AQx xx:
		        1N 2♣
		        2♦ 4N;
	}
}
