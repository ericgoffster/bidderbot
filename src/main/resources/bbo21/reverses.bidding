# Lebensohl over reverses

# C = ♣
# D = ♦
# H = ♥
# S = ♠

common
    1x 1y
    NJz:"reverse" {
    
	# Jump rebid first suit with gf hand and a powerful suit
	3y:"rev_resp" => 6+ y, 9+ tpts;
	    test KQJxxx xx Axx xx:
	        1♣ 1♠ 2♥ 3♠;
	    test AQTxxx xx Axx xx:
	        1♣ 1♠ 2♥ 3♠;
	
	# Rebid first suit with minimum hand and 5+ in that suit
	2y:"rev_resp" => 5+ y, 7+ tpts;
	    test KQJxx Qx xxxx xx:
	        1♣ 1♠
	        2♥ 2♠;
	
	# gf simple raise of first suit.
	NJx:"rev_resp" => 8+ fit x, gf;
	    test KQxx x xxxx Axxx:
	        1♣ 1♠ 2♥ 3♣;
	
	# slam inv with jump raise of first suit
	Jx:"rev_resp" => 8+ fit x, slaminv+;
	    test KQxx x Kxxx AKxx:
	        1♣ 1♠ 2♥ 4♣;
	
	# simple raise of second suit with gf+
	NJz:"rev_resp" => 8+ fit z, gf+;
	    test xxx KQxx Axxx xx:
	        1C 1H
	        2D 3D;
	
	# 4sf with generic game force
	NJw:"rev_resp" => gf;
	    test AQxx xxx xxxx Ax:
	        1♣ 1♠
	        2♥ 3♦;
	
	# 2N artficial weak
	2N:"rev_resp" => always;
	    test KQxx xx xxxx xxx:
	        1♣ 1♠
	        2♥ 2N;
	    test KQxxx xxx xxx xx:
	        1♣ 1♠
	        2♥ 2N;
	continuations {
		3z => 5+ z;
		    test - AQxxx KQ AKxxxx:
		        1♣ 1♠
		        2♥ 2N
		        3♥;
		
		3x => 6+ x;
		    test - AQxx AKxxxx KQx:
		        1♦ 1♠
		        2♥ 2N
		        3♦;
		
		3N => gf, stoppers unstopped;
		    test x AKJx AQxxx KQx:
		        1♦ 1♠
		        2♥ 2N
		        3N;
		
		3♣ => always;
		    test x AQxx KJx AQxxx:
		        1♣ 1♠
		        2♥ 2N
		        3♣;
		    test Kx AKJx AKxxx Qx:
		        1♦ 1♠
		        2♥ 2N
		        3♣;
    }
}