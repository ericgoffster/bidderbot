# 1N responses over 1N

# C = ♣
# D = ♦
# H = ♥
# S = ♠

common [] 1N:"nt_opener" {
	
	[[Bid stayman with 5-4 in the majors]]
	2♣:"stayman" => inv+, 5+M, 4OM;
	    test AJxxx Axxx xx xx:
	        1N 2♣;
	
	[[Simple transfer with 6+${M} and slam invitational values]]
	2M-1:"xfer" => slaminv, 6+M;
	    test AQxxxx x AKx Jxx:
	        1N 2♥;
	    test x AQxxxx AKx Jxx:
	        1N 2♦;
	
	[[Texas transfer with 6+${M} and game forcing values (or better)]]
	4M-1:"xfer" => 6+M, gf+;
	    test AQxxxx Kxx Qx xx:
	        1N 4♥;
	    test Kxx AQxxxx Qx xx:
	        1N 4♦;
	continuations {
	    [[Accept transfer]]
	    4M => always;
	    test AQxx AJxx Ax xxx:
	        1N 4♦ 
	        4♥;
	    test AQxx AJxx Ax xxx:
	        1N 4♥
	        4♠;
	}
	
	[[Simple transfer with 5+${M}]]
	2M-1:"xfer" => 5+M;
	    test Axxxx xxx Qxx xx:
	        1N 2♥;
	    test xxx Axxxx Qxx xx:
	        1N 2♦;
	
	[[Bid 3${x} with a singleton ${x} and 444 in the others]]
	3x:"splinter":>3C => gf+, 1-x, 4+w, 4+y, 4+z;
	    test Axxx Axxx x Axxx:
	        1N 3♦;
	    test Axxx x Axxx Axxx:
	        1N 3♥;
	    test x Axxx Axxx Axxx:
	        1N 3♠;
	
	[[Stayman with a 4 card major]]
	2♣:"stayman" => inv+, 4M;
	    test Axx AJxx xxxx xx:
	        1N 2♣;
	    test Axxx AJx xxxx xx:
	        1N 2♣;
	
	[[MSS with 9 cards in the minors]]
	2♠:"mss" => gf+, 5+m, 4+om;
	    test Axx x AKxxx AKxx:
	        1N 2♠;
	    test Axx x AKxx AKxxx:
	        1N 2♠;
	
	[[Bid 3N with game forcing values]]
	3N => gf;
	    test Axx Axx Qxxxx Kx:
	        1N 3N;
	
	[[MSS with 4-4 in the minors]]
	2♠:"mss" => gf+, 4+♣, 4+♦;
	    test Axx xx AKxx AKxx:
	        1N 2♠;
	
	
	[[Bid stayman with invitational values]]
	2♣:"stayman" => inv;
	    test Axx AJx xxxx xxx:
	        1N 2♣;
	
	[[Transfers to diamonds with 6]]
	3♣:"xfer" => 6+♦;
	    test x xxx QJxxxx xxx:
	        1N 3♣;
	
	[[Transfers to clubs with 6]]
	2N:"xfer" => 6+♣;
	    test x xxx xxx QJxxxx:
	        1N 2N;
}

include stayman.bidding;
include jacobyxfer.bidding;
include mss.bidding;
include minorxfer.bidding;
